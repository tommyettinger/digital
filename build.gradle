buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'
        constraints {
            classpath("org.apache.logging.log4j:log4j-core") {
                version {
                    strictly("[2.17, 3[")
                    prefer("2.17.0")
                }
                because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
            }
        }
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

// You may want to change this to 1.8 .
sourceCompatibility = 1.7

//Obviously, change the next line to match your project name.
def projectName = 'libgdx-library-template'

//Change this to whatever Maven Central group you might publish to, which is probably not this one if you aren't Tommy Ettinger.
group 'com.github.tommyettinger'

version "$VERSION_NAME"

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            // The -quiet is because of some sort of weird JDK JavaCompiler bug:
            // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
            options.addStringOption('Xdoclint:none,-missing', '-quiet')
        }
    }
}

if(JavaVersion.current().isJava9Compatible()) {
    allprojects {
        tasks.withType(JavaCompile) {
            options.release.set(7) // You can change this from 7 to 8 if you don't support iOS.
			                       // Using 7 triggers deprecation warnings on newer JDKs, but RoboVM needs it.
			                       // However, LWJGL3 needs Java 8 starting in 1.10.1-SNAPSHOT, so...
        }
    }
}

// JavaDocs will be published inside the docs/ folder, which you can easily put on GitHub Pages in your repo settings.
javadoc.destinationDir = file('docs/apidocs')

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    // You can uncomment mavenLocal() if you need self-built versions, but it can be a problem with GWT or other sources dependencies.
    //mavenLocal()
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    google()
    gradlePluginPortal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    // JitPack is a good repo to have if you depend on commit versions.
    // Having the below repo might cause problems if uploading to Maven Central.
    //maven { url 'https://jitpack.io' }
}

dependencies {
// Change gdxVersion in gradle.properties to update or downgrade.
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testImplementation "junit:junit:4.13.2"

    // Should be uncommented if you target Java 8, I think.
    // Should stay commented out if you target Java 7 (the default).
    //testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.2"

    constraints {
        implementation("org.apache.logging.log4j:log4j-core") {
            version {
                strictly("[2.17, 3[")
                prefer("2.17.0")
            }
            because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
        }
    }

}

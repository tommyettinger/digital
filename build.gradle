buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.34.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

def projectName = 'digital'
group = 'com.github.tommyettinger'
version = "$VERSION_NAME"

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
}

plainJavadocJar.archiveBaseName.set(projectName)

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc).tap {
            configureEach {
                // The -quiet is because of some sort of weird JDK JavaCompiler bug:
                // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
                options.addStringOption('Xdoclint:none,-missing', '-quiet')
            }
        }
    }
}

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

apply plugin: 'idea'
idea.module.excludeDirs += [file("src/main/java/com/github/tommyettinger/emu/"), file("docs/"), file("other/")]
[compileJava, compileTestJava, javadoc]*.exclude("com/github/tommyettinger/emu")

// JavaDocs will be published inside the docs/ folder, which you can easily put on GitHub Pages in your repo settings.
javadoc.destinationDir = file('docs/apidocs')

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    // You can uncomment mavenLocal() if you need self-built versions, but it can be a problem with GWT or other sources dependencies.
    mavenLocal()
    mavenCentral()
    google()
    // JitPack is a good repo to have if you depend on commit versions.
    // Having the below repo might cause problems if uploading to Maven Central.
    //maven { url = 'https://jitpack.io' }
}

dependencies {
    //noinspection GDXOutdatedVersionGradleGroovy
    testImplementation "com.badlogicgames.gdx:gdx:1.14.0"
    testImplementation "junit:junit:4.13.2"
    // don't require audio if a test doesn't need it; we don't use audio here.
    testImplementation ("com.badlogicgames.gdx:gdx-backend-lwjgl3:1.14.0"){
        exclude group: "org.jcraft", module: "jorbis"
        exclude group: "org.lwjgl", module: "lwjgl-openal"
    }
    testImplementation "com.badlogicgames.gdx:gdx-platform:1.14.0:natives-desktop"

}
